//    Copyright 2013 X35
//
//    This file is part of gamemod.
//
//    gamemod is free software: you can redistribute it and/or modify
//    it under the terms of the GNU General Public License as published by
//    the Free Software Foundation, either version 3 of the License, or
//    (at your option) any later version.
//
//    gamemod is distributed in the hope that it will be useful,
//    but WITHOUT ANY WARRANTY; without even the implied warranty of
//    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//    GNU General Public License for more details.
//
//    You should have received a copy of the GNU General Public License
//    along with gamemod.  If not, see <http://www.gnu.org/licenses/>.

// expressions starting with ?* are commands. take a look at guicommand.py.

?*PRIMCOLOR(6)
?*GUIDEBUG(0)
?*IFGUIDEBUG(RESET)
// cubescript-side version:
?*VERSION(18)
?*VC() gamemod ?*VERSION() masterserver script
?*NEWLINE()

?*FORSERVERS(TILEOL WHOLE addserver ?*IP() ?*PORT()?*NEWLINE())
?*IFGUIDEBUG(TILEOL addserver 127.0.0.1 28785?*NEWLINE())

alias gamemod_mastername "?*OWNADDRESS()";
alias gamemod_masterport ?*LISTENPORT();

gamemod_init = [ // called from autoexec.cfg if the user has written this into it. this is stored in config.cfg.
	mastername (getalias gamemod_mastername);
	masterport (getalias gamemod_masterport);
	updatefrommaster;
	if (getalias gamemod_cfg_showgui_servers_on_launch) [
		showgui servers;
	];
];

?*IFGUIDEBUG()

gamemod_convert_quickconnect_cfg = [ // convert quickconnect settings from older gamemod version
	if (! (getalias gamemod_cfg_quickconnect)) [ // don't overwrite any configuration
		looplist gamemod_c_row [1 2] [
			if (getalias (concatword "gamemod_enable_row_" $gamemod_c_row)) [ // this row was enabled in old gamemod config
				alias gamemod_c_rowi (gamemod_quickconnect_add_row);
				looplist gamemod_c_col [1 2 3 4 5 6 7 8 9 10] [
					alias gamemod_c_srv (getalias (concatword "gamemod_pos_" $gamemod_c_row "_" $gamemod_c_col));
					if (= $gamemod_c_srv -2) [ // custom server
						alias gamemod_c_ip (getalias (concatword "gamemod_custom_server_" $gamemod_c_row "_" $gamemod_c_col "_ip"));
						alias gamemod_c_port (getalias (concatword "gamemod_custom_server_" $gamemod_c_row "_" $gamemod_c_col "_port"));
						alias gamemod_c_name (getalias (concatword "gamemod_custom_server_" $gamemod_c_row "_" $gamemod_c_col "_name"));
						if (&& $gamemod_c_ip $gamemod_c_port (!=s $gamemod_c_name "")) [
							gamemod_quickconnect_add_block_with $gamemod_c_rowi (concatword "\"" (gamemod_safestr $gamemod_c_name) "\"") (concatword "\"" (gamemod_safestr $gamemod_c_ip) " " (gamemod_safestr $gamemod_c_port) "\"");
						];
					] [
						if (= $gamemod_c_srv 2) [ // CoE I & II, 188.40.118.113:28785 & 188.40.118.114:28785
							gamemod_quickconnect_add_block_with $gamemod_c_rowi ["CoE I" "CoE II"] ["188.40.118.113 28785" "188.40.118.114 28785"];
						] [;
							if (= $gamemod_c_srv 4) [
								gamemod_quickconnect_add_block_with $gamemod_c_rowi ["|noVI: CP" "|noVI: HP" "|noVI: UG"] ["62.75.213.175 1337" "62.75.213.175 30000" "62.75.213.175 28785"];
							] [
								if (= $gamemod_c_srv 5) [
									gamemod_quickconnect_add_block_with $gamemod_c_rowi ["oo| 1" "oo| 2" "oo| 3"] ["81.169.137.114 10000" "81.169.137.114 20000" "81.169.137.114 30000"];
								] [
									if (= $gamemod_c_srv 6) [
										gamemod_quickconnect_add_block_with $gamemod_c_rowi ["PSL 1" "PSL 2" "PSL 3" "PSL 4"] ["85.214.66.181 10000" "85.214.66.181 20000" "85.214.66.181 30000" "85.214.66.181 40000"];
									] [
										if (= $gamemod_c_srv 8) [
											gamemod_quickconnect_add_block_with $gamemod_c_rowi ["}TC{ 1" "}TC{ 2" "}TC{ 3"] ["62.75.191.99 10010" "62.75.191.99 10020" "62.75.191.99 10030"];
										] [
											if (= $gamemod_c_srv 9) [
												gamemod_quickconnect_add_block_with $gamemod_c_rowi ["NL 4" "NL 5" "MapLounge"] ["176.9.75.98 10030" "176.9.75.98 10040" "176.9.75.98 11110"];
											] [
												if (= $gamemod_c_srv 10) [
													gamemod_quickconnect_add_block_with $gamemod_c_rowi ["SWL 1" "SWL 2" "SWL 3" "SWL 4"] ["5.39.108.236 10000" "5.39.108.236 20000" "5.39.108.236 30000" "5.39.108.236 40000"];
												];
											];
										];
									];
								];
							];
						];
					];
				];
			];
		];
		alias gamemod_c_srv "";
		alias gamemod_c_rowi "";
		alias gamemod_c_blocki "";
		alias gamemod_c_serverinfo_1 "";
		alias gamemod_c_elementvarname "";
		alias gamemod_c_ip "";
		alias gamemod_c_port "";
		alias gamemod_c_name "";
	];
];

?*IFGUIDEBUG()

gamemod_defaultcfg = [ // called every update, before onrequest
	if (|| $arg1 (=s (getalias gamemod_cfg_maxsearchelements) "")) [ alias gamemod_cfg_maxsearchelements 8; ];
	if (|| $arg1 (=s (getalias gamemod_cfg_gui_servers_show_clientcounts) "")) [ alias gamemod_cfg_gui_servers_show_clientcounts 1; ];
	if (|| $arg1 (=s (getalias gamemod_cfg_gui_servers_show_servercounts) "")) [ alias gamemod_cfg_gui_servers_show_servercounts 1; ];
	
	if (|| $arg1 (=s (getalias gamemod_cfg_gui_servers_refresh_button_blink_interval) "")) [ alias gamemod_cfg_gui_servers_refresh_button_blink_interval 1000; ];
	if (|| $arg1 (=s (getalias gamemod_cfg_gui_servers_refresh_button_blink) "")) [ alias gamemod_cfg_gui_servers_refresh_button_blink 1; ];
	if (|| $arg1 (=s (getalias gamemod_cfg_gui_servers_refresh_button_blink_after) "")) [ alias gamemod_cfg_gui_servers_refresh_button_blink_after 20000; ];
	
	if (|| $arg1 (=s (getalias gamemod_cfg_gui_servers_autorefresh) "")) [ alias gamemod_cfg_gui_servers_refresh_button_blink_interval 0; ];
	if (|| $arg1 (=s (getalias gamemod_cfg_gui_servers_autorefresh_after) "")) [ alias gamemod_cfg_gui_servers_autorefresh_after 30000; ];
	
	if (|| $arg1 (=s (getalias gamemod_gui_servers_filter_open) "")) [ alias gamemod_gui_servers_filter_open 0; ];
	if (|| $arg1 (=s (getalias gamemod_gui_servers_filter_open_hide) "")) [ alias gamemod_gui_servers_filter_open_hide 0; ];
	
	if (|| $arg1 (=s (getalias gamemod_cfg_ignore_wrong_domain_warning) "")) [ alias gamemod_cfg_ignore_wrong_domain_warning 0; ];
];

?*IFGUIDEBUG()

gamemod_onrequest = [ // called every update, after defaultcfg and before servers are added
	alias gamemod_list_servers "";
	alias gamemod_servervar_n 0;
	if (! (getalias gamemod_notnew)) [ gamemod_convert_quickconnect_cfg; ];
	gamemod_guipart_update_wrong_domain_warning;
];

?*IFGUIDEBUG()

gamemod_onfinishrequest = [ // called every update, after onrequest and after were added	
	gamemod_gui_searchupdate;
	gamemod_gui_quickconnectupdate;
	alias gamemod_lastupdatemillis (getmillis);
	alias gamemod_notnew 1;
	alias gamemod_gui_quickconnect_config_show_help 0;
	
	if (getalias gamemod_gui_servers_filter_open) [
		gamemod_filter_open_switch 1;
	];
];

?*IFGUIDEBUG()

gamemod_reset = [ // reset configuration
	gamemod_quickconnect_clear;
	
	alias gamemod_v5_seen_val 0;
	alias gamemod_cfg_showgui_servers_on_launch 0;
	
	gamemod_defaultcfg 1;
	
	gamemod_gui_quickconnectupdate 1;
];

?*IFGUIDEBUG()

gamemod_as = [
	// args: ip, port, [clients maxclients mastermode gamemode remaining], map, desc, playerlist
	// playerlist: ["name cn team priv state" ...]
	
	alias gamemod_c_serverinfo_var (concatword "gamemod_serverinfo_" $gamemod_servervar_n);
	alias gamemod_c_servermap_var (concatword "gamemod_servermap_" $gamemod_servervar_n);
	alias gamemod_c_serverdesc_var (concatword "gamemod_serverdesc_" $gamemod_servervar_n);
	alias gamemod_c_servergui_name (concatword "gamemod_servergui_" $arg1 "_" $arg2); // servergui names are ip-port-based (makes updating easier and guis aren't stored anyway)
	alias gamemod_c_playerlist_var (concatword "gamemod_playerlist_" $gamemod_servervar_n);
	
	alias $gamemod_c_serverinfo_var (concat $arg1 $arg2 $arg3 $gamemod_c_servermap_var $gamemod_c_serverdesc_var $gamemod_c_servergui_name $gamemod_c_playerlist_var);
	alias (concatword "gamemod_serverinfo_" $arg1 "_" $arg2) (concat $arg1 $arg2 $arg3 $gamemod_c_servermap_var $gamemod_c_serverdesc_var);
	
	alias $gamemod_c_servermap_var $arg4;
	alias $gamemod_c_serverdesc_var $arg5;
	
	alias $gamemod_c_playerlist_var $arg6;
	
	gamemod_append gamemod_list_servers $gamemod_c_serverinfo_var;
	
	alias gamemod_c_serverinfo_var "";
	alias gamemod_c_servermap_var "";
	alias gamemod_c_serverdesc_var "";
	alias gamemod_c_servergui_name "";
	alias gamemod_c_playerlist_var "";
	
	alias gamemod_servervar_n (+ $gamemod_servervar_n 1);
];

gamemod_find_serverinfovar = [
	alias gamemod_c_serverinfovar "";
	looplist gamemod_c_e (getalias gamemod_list_servers) [
		if (! $gamemod_c_serverinfovar) [
			alias gamemod_c_serverinfo (getalias $gamemod_c_e);
			if (&& (=s $arg1 (gamemod_getip $gamemod_c_serverinfo)) (= $arg2 (gamemod_getport $gamemod_c_serverinfo))) [
				alias gamemod_c_serverinfovar $gamemod_c_e;
			];
		];
	];
	result $gamemod_c_serverinfovar;
];

gamemod_find_serverinfo = [
	alias gamemod_c_serverinfovar (gamemod_find_serverinfovar $arg1 $arg2);
	if $gamemod_c_serverinfovar [
		result (getalias $gamemod_c_serverinfovar);
	] [
		result "";
	];
];

gamemod_getip = [
	at $arg1 0;
];

gamemod_getport = [
	at $arg1 1;
];

gamemod_getmap = [
	getalias (at $arg1 7);
];

gamemod_getmode = [
	at $arg1 5;
];

gamemod_getmodename = [
	gamemod_modename (gamemod_getmode $arg1);
];

gamemod_getdesc = [
	getalias (at $arg1 8);
];

gamemod_getclients = [
	at $arg1 2;
];

gamemod_getmaxclients = [
	at $arg1 3;
];

gamemod_getmastermode = [
	at $arg1 4;
];

gamemod_getremaining = [
	at $arg1 6;
];

gamemod_getguiname = [
	at $arg1 9;
];

gamemod_getplayerlistvar = [
	at $arg1 10;
];

gamemod_getplayerlist = [
	getalias (gamemod_getplayerlistvar $arg1);
];

gamemod_makeservergui = [ // return server gui guicode, args: serverinfo (content)
	
	alias gamemod_c_guicode "guinoautotab ["; // prevent this gui from being auto-tabbed
	
	gamemod_append gamemod_c_guicode "guilist [";
		gamemod_append gamemod_c_guicode "guistayopen [";
			gamemod_append gamemod_c_guicode (concatword "guibutton \"?*PRIMCOLOR()>?*CLEAR()refresh\" [updatefrommaster; gamemod_updateservergui_ip_port " (gamemod_getip $arg1) " " (gamemod_getport $arg1) "] 0;");
		gamemod_append gamemod_c_guicode "];";
		gamemod_append gamemod_c_guicode "guibar;";
		gamemod_append gamemod_c_guicode (concatword "guitext \"?*GREY()" (gamemod_getip $arg1) ":" (gamemod_getport $arg1) "?*CLEAR()\" 0;");
	gamemod_append gamemod_c_guicode "];";
	gamemod_append gamemod_c_guicode "guibar;";
	gamemod_append gamemod_c_guicode "guilist [";
		gamemod_append gamemod_c_guicode (concatword "guitext \"" (gamemod_getcc $arg1) "\" 0;");
		gamemod_append gamemod_c_guicode "guibar;";
		gamemod_append gamemod_c_guicode (concatword "guibutton \"?*PRIMCOLOR()>>?*CLEAR() " (gamemod_getdesc $arg1) "\" [connect " (gamemod_getip $arg1) " " (gamemod_getport $arg1) "] 0;");
	gamemod_append gamemod_c_guicode "];";
	gamemod_append gamemod_c_guicode "guibar;";
	gamemod_append gamemod_c_guicode "guilist [";
		gamemod_append gamemod_c_guicode (concatword "guitext \"" (gamemod_representmastermode $arg1) "\" 0;");
		gamemod_append gamemod_c_guicode "guibar;";
		gamemod_append gamemod_c_guicode (concatword "guitext \"?*WHITE()" (gamemod_getmodename $arg1) "?*CLEAR()\" 0;");
		gamemod_append gamemod_c_guicode "guibar;";
		gamemod_append gamemod_c_guicode (concatword "guitext \"?*WHITE()" (gamemod_getmap $arg1) "?*CLEAR()\" 0;");
		gamemod_append gamemod_c_guicode "guibar;";
		gamemod_append gamemod_c_guicode (concatword "guitext (concatword \"?*WHITE()\" (gamemod_remaining_time_current " (gamemod_getremaining $arg1) ") \"?*CLEAR()\") 0;");
	gamemod_append gamemod_c_guicode "];";
	
	gamemod_i;
	gamemod_a gamemod_c_playerlist (gamemod_getplayerlist $arg1);
	gamemod_a gamemod_c_playerlistname;
	
	if (> (listlen $gamemod_c_playerlist) 0) [ // server has clients in it's playerlist
		// add player list to gui:
		gamemod_a gamemod_c_teamslist ""; // list of team lists (in format: ["name var" "name var" ...])
		gamemod_a gamemod_c_speclist ""; // list of spectators
		// used inside loop:
		gamemod_a gamemod_c_team "";
		gamemod_a gamemod_c_playerlistname "";
		gamemod_a gamemod_c_n_teamlist 0;
		
		gamemod_append gamemod_c_guicode "guibar;";
	
		looplist gamemod_c_playerinfo $gamemod_c_playerlist [
			if (gamemod_getplayerisspectator $gamemod_c_playerinfo) [
				alias gamemod_c_playerlistname "gamemod_c_speclist"; // make command below append this player to spectator list
			] [
				alias gamemod_c_team (gamemod_getplayerteam $gamemod_c_playerinfo);
		
				alias gamemod_c_playerlistname "";
				looplist gamemod_c_teamitem $gamemod_c_teamslist [
					if (&& (! (getalias gamemod_c_playerlistname)) (=s (at $gamemod_c_teamitem 0) $gamemod_c_team)) [
						alias gamemod_c_playerlistname (at $gamemod_c_teamitem 1);
					];
				];
				if (=s (getalias gamemod_c_playerlistname) "") [ // create new list for this team and append it to teams list
					alias gamemod_c_playerlistname (concatword "gamemod_c_teamlist_" $gamemod_c_n_teamlist);
					alias gamemod_c_n_teamlist (+ $gamemod_c_n_teamlist 1);
					alias $gamemod_c_playerlistname "";
					gamemod_append gamemod_c_teamslist (concatword "\"" $gamemod_c_team " " $gamemod_c_playerlistname "\""); // append it to teams list
				];
			];
			gamemod_append $gamemod_c_playerlistname (concatword "\"" (gamemod_representplayer $gamemod_c_playerinfo) "\"");
		];
	
		gamemod_a gamemod_c_n 0; // number of current team list (if >= 3 (after 4 teams in a row), jump below and reset to 0)
		gamemod_a gamemod_c_name "";
		gamemod_a gamemod_c_listvar "";
	
		looplist gamemod_c_teaminfo $gamemod_c_teamslist [
			if (= $gamemod_c_n 0) [ // first team in this row. open guilist (guialign 0).
				gamemod_append gamemod_c_guicode "guialign 0 [";
			] [
				if (> $gamemod_c_n 0) [ // not the first team in this row. append a (vertical) guibar.
					gamemod_append gamemod_c_guicode "guibar;";
				];
			];
		
			alias gamemod_c_team (at $gamemod_c_teaminfo 0);
			alias gamemod_c_listvar (at $gamemod_c_teaminfo 1);
		
			gamemod_append gamemod_c_guicode "guilist ["; // make everything vertical again
		
				gamemod_append gamemod_c_guicode "guialign 0 [";
					gamemod_append gamemod_c_guicode (concatword "guitext \"?*GREY()[?*CLEAR() ?*PRIMCOLOR()" $gamemod_c_team "?*CLEAR() ?*GREY()]?*CLEAR()\" 0;");
				gamemod_append gamemod_c_guicode "];"; // guialign 0
			
				looplist gamemod_c_name (getalias $gamemod_c_listvar) [
					gamemod_append gamemod_c_guicode (concatword "guitext \"" $gamemod_c_name "\" 0;");
				];
		
			gamemod_append gamemod_c_guicode "];"; // guilist
		
			if (>= $gamemod_c_n 3) [ // this was the 4th team in this row. close guilist (guialign 0) and set gamemod_c_n to 0.
				gamemod_append gamemod_c_guicode "];"; // guilist (guialign 0)
				alias gamemod_c_n 0;
			] [
				alias gamemod_c_n (+ $gamemod_c_n 1);
			];
		];
		if (!= $gamemod_c_n 0) [ // horizontal-making guilist has not been closed. close it.
			gamemod_append gamemod_c_guicode "];"; // guilist
		];
	
		if (> (listlen $gamemod_c_speclist) 0) [
			if (> (listlen $gamemod_c_teamslist) 0) [ // if there are teams displayed above this, add guibar
				gamemod_append gamemod_c_guicode "guibar;";
			];
	
			gamemod_append gamemod_c_guicode "guialign 0 [";
				gamemod_append gamemod_c_guicode "guitext \"?*GREY()[ spectators ]?*CLEAR()\" 0;";
			gamemod_append gamemod_c_guicode "];"; // guialign 0
		
			looplist gamemod_c_name $gamemod_c_speclist [
				gamemod_append gamemod_c_guicode (concatword "guitext \"" $gamemod_c_name "\" 0;");
			];
		];
	
		// done adding player list to gui
	
	] [ // server does not have clients in it's playerlist
		if (> (gamemod_getclients $arg1) 0) [ // but server announces present clients at normal info request -> server does not support extinfo
			gamemod_append gamemod_c_guicode "guibar;guitext \"?*GREY()this server does not support extended information\" 0;";
		];
	];
	
	gamemod_c;
	
	gamemod_append gamemod_c_guicode "];"; // guinoautotab // prevent this gui from being auto-tabbed
	
	result (gamemod_resultalias gamemod_c_guicode);
];




gamemod_getplayerteam = [
	at $arg1 2;
];

gamemod_getplayername = [
	at $arg1 0;
];

gamemod_getplayercn = [
	at $arg1 1;
];

gamemod_getplayerpriv = [
	at $arg1 3;
];

gamemod_getplayerstate = [
	at $arg1 4;
];

gamemod_getplayerisspectator = [
	result (= (gamemod_getplayerstate $arg1) 5);
];




gamemod_getplayercolor = [
	alias gamemod_c_priv (gamemod_getplayerpriv $arg1);
	result (gamemod_rc (? (> $gamemod_c_priv 0) (? (>= $gamemod_c_priv 2) "?*ORANGE()" "?*GREEN()") "?*WHITE()") gamemod_c_priv);
];

gamemod_representplayer = [
	alias gamemod_c_cn (gamemod_getplayercn $arg1);
	result (gamemod_rc (concatword (gamemod_getplayercolor $arg1) (gamemod_getplayername $arg1) (? (>= $gamemod_c_cn 128) (concatword " ?*MAGENTA()[" $gamemod_c_cn "]?*CLEAR()") "")) [gamemod_c_cn]);
];

gamemod_representmastermode = [
	alias gamemod_c_m (gamemod_getmastermode $arg1);
	result (gamemod_rc (concatword (gamemod_mastermodecolor $gamemod_c_m) (gamemod_mastermodename $gamemod_c_m) "?*CLEAR()") [gamemod_c_m]);
];

gamemod_mastermodenames = [open veto locked private password];
gamemod_mastermodename = [
	if (= $arg1 -1) [
		result "auth";
	] [
		if (&& (>= $arg1 0) (< $arg1 (listlen (getalias gamemod_mastermodenames)))) [
			result (at (getalias gamemod_mastermodenames) $arg1);
		] [
			result (concatword "unknown mastermode (" $arg1 ")");
		];
	];
];

gamemod_mastermodecolors = ["?*GREEN()" "?*YELLOW()" "?*YELLOW()" "?*RED()" "?*RED()"];
gamemod_mastermodecolor = [
	if (&& (>= $arg1 0) (< $arg1 (listlen (getalias gamemod_mastermodecolors)))) [
		result (at (getalias gamemod_mastermodecolors) $arg1);
	] [
		result "?*WHITE()";
	];
];

gamemod_time = [ // return a string representing the given time (which is in seconds)
	gamemod_i;
	gamemod_a gamemod_c_arg (max $arg1 0);
	gamemod_a gamemod_c_minutes (div $gamemod_c_arg 60);
	gamemod_a gamemod_c_seconds (mod $gamemod_c_arg 60);
	gamemod_a gamemod_c_res (concatword $gamemod_c_minutes ":" $gamemod_c_seconds " (" $gamemod_c_arg ")");
	result (gamemod_c (concatword (? (< $gamemod_c_minutes 10) "0" "") $gamemod_c_minutes ":" (? (< $gamemod_c_seconds 10) "0" "") $gamemod_c_seconds));
];

gamemod_remaining_time_current = [ // return current remaining time string, args: remaining time in seconds at point of update
	result (gamemod_time (- $arg1 (div (- (getmillis) (getalias gamemod_lastupdatemillis)) 1000))); // remainingtime - ((nowtime - lastupdatetime) / 1000)
];

?*IFGUIDEBUG()

gamemod_safestr = [
	alias gamemod_c_a $arg1;
	alias gamemod_c_a (strreplace $gamemod_c_a "^^" "");
	alias gamemod_c_a (strreplace $gamemod_c_a "\"" "");
	result (gamemod_resultalias gamemod_c_a);
];

gamemod_tolowercase = [
	alias gamemod_c_a $arg1;
	alias gamemod_c_a (strreplace $gamemod_c_a "A" "a");
	alias gamemod_c_a (strreplace $gamemod_c_a "B" "b");
	alias gamemod_c_a (strreplace $gamemod_c_a "C" "c");
	alias gamemod_c_a (strreplace $gamemod_c_a "D" "d");
	alias gamemod_c_a (strreplace $gamemod_c_a "E" "e");
	alias gamemod_c_a (strreplace $gamemod_c_a "F" "f");
	alias gamemod_c_a (strreplace $gamemod_c_a "G" "g");
	alias gamemod_c_a (strreplace $gamemod_c_a "H" "h");
	alias gamemod_c_a (strreplace $gamemod_c_a "I" "i");
	alias gamemod_c_a (strreplace $gamemod_c_a "J" "j");
	alias gamemod_c_a (strreplace $gamemod_c_a "K" "k");
	alias gamemod_c_a (strreplace $gamemod_c_a "L" "l");
	alias gamemod_c_a (strreplace $gamemod_c_a "M" "m");
	alias gamemod_c_a (strreplace $gamemod_c_a "N" "n");
	alias gamemod_c_a (strreplace $gamemod_c_a "O" "o");
	alias gamemod_c_a (strreplace $gamemod_c_a "P" "p");
	alias gamemod_c_a (strreplace $gamemod_c_a "Q" "q");
	alias gamemod_c_a (strreplace $gamemod_c_a "R" "r");
	alias gamemod_c_a (strreplace $gamemod_c_a "S" "s");
	alias gamemod_c_a (strreplace $gamemod_c_a "T" "t");
	alias gamemod_c_a (strreplace $gamemod_c_a "U" "u");
	alias gamemod_c_a (strreplace $gamemod_c_a "V" "v");
	alias gamemod_c_a (strreplace $gamemod_c_a "W" "w");
	alias gamemod_c_a (strreplace $gamemod_c_a "X" "x");
	alias gamemod_c_a (strreplace $gamemod_c_a "Y" "y");
	alias gamemod_c_a (strreplace $gamemod_c_a "Z" "z");
	result (gamemod_resultalias gamemod_c_a);
];

gamemod_modename = [
	if (< $arg1 0) [ result "unknown"; ] [
		result (at ["ffa" "coop edit" "teamplay" "instagib" "instagib team" "efficiency" "efficiency team" "tactics" "tactics team" "capture" "regen capture" "ctf" "insta ctf" "protect" "insta protect" "hold" "insta hold" "efficiency ctf" "efficiency protect" "efficiency hold"] $arg1);
	];
];

?*IFGUIDEBUG()

gamemod_append = [
	alias $arg1 (concatword (getalias $arg1) (? (> (strlen (getalias $arg1)) 0) " " "") $arg2);
];

gamemod_i = [ // init new alias tracking list (and copy old one for later restore)
	if (&& (!=s (getalias gamemod_aliastracking) "") (!=s (getalias gamemod_aliastracking) "0")) [
		alias gamemod_c_newname (gamemod_getvarname "gamemod_aliastracking_");
		alias $gamemod_c_newname $gamemod_aliastracking; // copy current tracking list into new var for later restore
		alias gamemod_aliastracking $gamemod_c_newname;
		alias gamemod_c_newname "";
	] [
		alias gamemod_aliastracking "0";
	];
];

gamemod_a = [ // new tracked alias
	gamemod_append gamemod_aliastracking $arg1;
	alias $arg1 $arg2;
];

gamemod_c = [ // clear tracked aliases (and return arg1 if given)
	alias gamemod_c_c_i 0;
	alias gamemod_c_oldlist "";
	looplist a (getalias gamemod_aliastracking) [
		if (= $gamemod_c_c_i 0) [
			alias gamemod_c_oldlist $a; // keep var name containing old list
		] [
			alias $a "";
		];
		alias gamemod_c_c_i (+ $gamemod_c_c_i 1);
	];
	if (&& (!=s $gamemod_c_oldlist "") (!=s $gamemod_c_oldlist "0")) [
		alias gamemod_aliastracking (getalias $gamemod_c_oldlist); // restore old list
	] [
		alias gamemod_aliastracking ""; // delete list
	];
	alias gamemod_c_c_i "";
	alias gamemod_c_oldlist "";
	//if $arg1 [ result $arg1; ];
	result $arg1;
];

?*IFGUIDEBUG()

gamemod_resultalias = [ // return the given varname and clear it
	result (gamemod_rc (getalias $arg1) $arg1);
];

gamemod_rc = [ // return the given string (arg1) and clear the varnames in list (arg2)
	looplist gamemod_c_vn $arg2 [
		alias $gamemod_c_vn "";
	];
	result $arg1;
];

?*IFGUIDEBUG()

gamemod_guipart_update_wrong_domain_warning = [
	if (&& (=s $mastername "is.kicks-ass.org") (! (getalias gamemod_cfg_ignore_wrong_domain_warning))) [
		alias gamemod_guipart_wrong_domain_warning [
			guitext "?*PRIMCOLOR()Warning:?*CLEAR() ?*WHITE()You're using an old domain ?*GREY()(is.kicks-ass.org)?*CLEAR() in your mastername setting, this is not recommended.?*CLEAR()" 0;
			guitext "?*WHITE()Please find the according line in your autoexec.cfg ?*GREY()(main menu -> options -> autoexec.cfg)?*CLEAR() and change ?*PRIMCOLOR()is.kicks-ass.org?*CLEAR() to ?*PRIMCOLOR()master.crapmod.net?*CLEAR()?*CLEAR()" 0;
			guistayopen [
				guibutton "?*PRIMCOLOR()>?*CLEAR()ignore" [alias gamemod_cfg_ignore_wrong_domain_warning 1; gamemod_guipart_update_wrong_domain_warning] 0;
			];
			guibar;
		];
	] [
		alias gamemod_guipart_wrong_domain_warning "";
	];
];

?*IFGUIDEBUG()

newgui gamemod_gui_about [
	guialign 0 [
		guitext "?*PRIMCOLOR()crapmod?*CLEAR() ?*WHITE()masterserver?*CLEAR()" 0;
		guibar;
		guitext "?*WHITE()version?*CLEAR() ?*GREY()?*VERSION()?*CLEAR()" 0;
	];
	guibar;
	
	guilist [
		guilist [
			guitext "?*WHITE()contact?*CLEAR()" 0;
			guitext "" 0;
			guitext "" 0;
			guibar;
			guitext "?*WHITE()developer?*CLEAR()" 0;
			guibar;
			guitext "?*WHITE()guisize?*CLEAR()" 0;
		];
		guibar;
		guilist [
			guitext "?*WHITE()http:/?*PASS(/)?*CLEAR()?*PRIMCOLOR()crapmod.net?*CLEAR()?*WHITE()/?*CLEAR()" 0;
			guitext "?*WHITE()http:/?*PASS(/)facebook.com/?*CLEAR()?*PRIMCOLOR()crapmod?*CLEAR()" 0;
			guitext "?*PRIMCOLOR()#crapmod?*CLEAR() ?*WHITE()on?*CLEAR() ?*PRIMCOLOR()irc.gamesurge.net?*CLEAR()" 0;
			guibar;
			guitext "?*PRIMCOLOR()X35?*CLEAR()" 0;
			guibar;
			guitext (concatword "?*PRIMCOLOR()" (getalias gamemod_guisize) "?*CLEAR()?*WHITE()B?*CLEAR()") 0;
		];
	];
	
	guibar;
	
	guialign 0 [
		guitext "?*GREY()statistics:?*CLEAR()" 0;
	];
	guialign 0 [
		guitext "?*PRIMCOLOR()?*REQUESTS()?*CLEAR() ?*WHITE()requests from?*CLEAR() ?*PRIMCOLOR()?*DIFFERENTIPS()?*CLEAR() ?*WHITE()different IPs during the past 24 hours?*CLEAR()" 0;
	];
] "about";

newgui gamemod_gui_install [
	guibutton "back?*PRIMCOLOR()<?*CLEAR()" "cleargui 1" 0;
	guibar;
	guitext "?*WHITE()to make gamemod persist as your masterserver setting,?*CLEAR()" 0;
	guitext "?*WHITE()you will need to add the ?*PRIMCOLOR()following, purple command?*CLEAR() to your ?*PRIMCOLOR()autoexec.cfg?*CLEAR() file:?*CLEAR()" 0;
	guibar;
	guitext "?*MAGENTA()gamemod_init?*CLEAR()" 0;
	guibar;
	guitext "?*WHITE()the editable field below is your ?*PRIMCOLOR()autoexec.cfg?*CLEAR() file.?*CLEAR()" 0;
	guitext "?*WHITE()click at the field and write down the purple command above. make sure it is placed in it's ?*PRIMCOLOR()own line?*CLEAR().?*CLEAR()" 0;
	guitext "?*WHITE()finally, ?*PRIMCOLOR()after you've written down the purple command above?*CLEAR(),?*CLEAR()" 0;
	guitext "?*WHITE()click the ?*PRIMCOLOR()save & go back?*CLEAR() button below the textfield to save the changes and go back.?*CLEAR()" 0;
	guitext "?*WHITE()that's it.?*CLEAR()" 0;
	guibar;
	guinoautotab [
		guieditor "autoexec.cfg" -80 20;
		guibar;
		guibutton "?*PRIMCOLOR()>?*CLEAR()save & go back ?*GREY()(last step)?*CLEAR()" [textfocus "autoexec.cfg"; textsave "autoexec.cfg"; cleargui 1] 0;
	];
	if (getalias gamemod_c_gui_install_load) [
		textfocus "autoexec.cfg";
		textload "autoexec.cfg";
		alias gamemod_c_gui_install_load "";
	];
] "install";

gamemod_showgui_install = [
	alias gamemod_c_gui_install_load 1; // tell gui to load autoexec now
	showgui gamemod_gui_install;
];

gamemod_loopfromto = [ // args: list from_index to_index var code
	?*IFGUIDEBUG(TILEOL echo (concatword "gamemod_loopfromto: looping a list with length " (listlen $arg1) " from index " $arg2 " to " $arg3 ", var = \"" $arg4 "\"");)

	alias gamemod_loopfromto_c_index $arg2;
	
	while [<= $gamemod_loopfromto_c_index $arg3] [
		alias $arg4 (at $arg1 $gamemod_loopfromto_c_index);
		arg5;
		alias gamemod_loopfromto_c_index (+ $gamemod_loopfromto_c_index 1);
	];
	
	alias $arg4 "";
	alias gamemod_loopfromto_c_index "";
];

gamemod_getguicode_serverlist = [
	?*IFGUIDEBUG(TILEOL echo (concatword "gamemod_getguicode_serverlist: \"" $arg1 "\" \"" $arg2 "\" \"" $arg3 "\" \"" $arg4 "\" \"" $arg5 "\"");)
	
	alias gamemod_c_guicode "";
	if $arg5 [ // searching enabled
		alias gamemod_c_list (gamemod_search_server (getalias gamemod_gui_serverlist_searchterm));
	] [
		alias gamemod_c_list (getalias gamemod_list_servers);
	];
	alias gamemod_c_listlen (listlen $gamemod_c_list);
	alias gamemod_c_i 0;
	alias gamemod_c_linesperpage $guiautotab;
	alias gamemod_c_command $arg2;
	alias gamemod_c_pagefill 3; // search box takes 2 lines on first page, column descriptions take 1 line
	alias gamemod_c_tabnum 1;
	
	// insert searchbox:
	gamemod_append gamemod_c_guicode "guistayopen [";
	if $arg5 [
		gamemod_append gamemod_c_guicode "guilist [";
		gamemod_append gamemod_c_guicode (concatword "guifield gamemod_gui_serverlist_searchterm 15 [newgui " $arg3 " (gamemod_getguicode_serverlist \"" $arg1 "\" \"" $arg2 "\" \"" $arg3 "\" \"" $arg4 "\" 1) \"" $arg4 "\"];");
		gamemod_append gamemod_c_guicode "guibar;";
		gamemod_append gamemod_c_guicode (concatword "guibutton \"close?*PRIMCOLOR()<?*CLEAR()\" [newgui " $arg3 " (gamemod_getguicode_serverlist \"" $arg1 "\" \"" $arg2 "\" \"" $arg3 "\" \"" $arg4 "\") \"" $arg4 "\"] 0;");
		gamemod_append gamemod_c_guicode "];"; // guilist [
	] [
		alias gamemod_gui_serverlist_searchterm "";
		gamemod_append gamemod_c_guicode (concatword "guibutton \"?*PRIMCOLOR()>?*CLEAR()search\" [newgui " $arg3 " (gamemod_getguicode_serverlist \"" $arg1 "\" \"" $arg2 "\" \"" $arg3 "\" \"" $arg4 "\" 1) \"" $arg4 "\"] 0;");
	];
	gamemod_append gamemod_c_guicode "];"; // guistayopen [
	gamemod_append gamemod_c_guicode "guibar;";
	
	while [< $gamemod_c_i $gamemod_c_listlen] [
	
		if (!= $gamemod_c_i 0) [
			alias gamemod_c_tabnum (+ $gamemod_c_tabnum 1);
			gamemod_append gamemod_c_guicode (concatword "guitab " $gamemod_c_tabnum ";");
		];
		
		alias gamemod_c_max_i (max (min (- (+ $gamemod_c_i (- $gamemod_c_linesperpage $gamemod_c_pagefill)) 1) (- $gamemod_c_listlen 1)) 0);
		
		gamemod_append gamemod_c_guicode "guilist [";
		
		looplist gamemod_c_col $arg1 [
			gamemod_append gamemod_c_guicode "guilist [";
			
			if (=s $gamemod_c_col "players") [
				if (= $gamemod_c_i 0) [ gamemod_append gamemod_c_guicode (concatword "guitext \"?*GREY()" $gamemod_c_col " ?*CLEAR()\" 0;"); ];
				gamemod_loopfromto $gamemod_c_list $gamemod_c_i $gamemod_c_max_i gamemod_c_serverinfo [
					gamemod_append gamemod_c_guicode (concatword "guibutton \"" (gamemod_getcc (getalias $gamemod_c_serverinfo)) " \" \"alias gamemod_gui_serverlist_searchterm [];" $gamemod_c_command " (getalias " $gamemod_c_serverinfo ")\" 0;");
					?*IFGUIDEBUG(TILEOL echo (concatword "guibutton \"" (gamemod_getcc (getalias $gamemod_c_serverinfo)) " \" \"alias gamemod_gui_serverlist_searchterm [];" $gamemod_c_command " (getalias " $gamemod_c_serverinfo ")\" 0;");)
				];
			] [
			
			if (=s $gamemod_c_col "mastermode") [
				if (= $gamemod_c_i 0) [ gamemod_append gamemod_c_guicode "guitext \"?*GREY()master ?*CLEAR()\" 0;"; ];
				gamemod_loopfromto $gamemod_c_list $gamemod_c_i $gamemod_c_max_i gamemod_c_serverinfo [
					gamemod_append gamemod_c_guicode (concatword "guibutton \"" (gamemod_representmastermode (getalias $gamemod_c_serverinfo)) " \" \"alias gamemod_gui_serverlist_searchterm [];" $gamemod_c_command " (getalias " $gamemod_c_serverinfo ")\" 0;");
				];
			] [
	
			if (=s $gamemod_c_col "map") [
				if (= $gamemod_c_i 0) [ gamemod_append gamemod_c_guicode (concatword "guitext \"?*GREY()" $gamemod_c_col " ?*CLEAR()\" 0;"); ];
				gamemod_loopfromto $gamemod_c_list $gamemod_c_i $gamemod_c_max_i gamemod_c_serverinfo [
					gamemod_append gamemod_c_guicode (concatword "guibutton \"" (gamemod_getmap (getalias $gamemod_c_serverinfo)) " \" \"alias gamemod_gui_serverlist_searchterm [];" $gamemod_c_command " (getalias " $gamemod_c_serverinfo ")\" 0;");
				];
			] [
	
			if (=s $gamemod_c_col "mode") [
				if (= $gamemod_c_i 0) [ gamemod_append gamemod_c_guicode (concatword "guitext \"?*GREY()" $gamemod_c_col " ?*CLEAR()\" 0;"); ];
				gamemod_loopfromto $gamemod_c_list $gamemod_c_i $gamemod_c_max_i gamemod_c_serverinfo [
					gamemod_append gamemod_c_guicode (concatword "guibutton \"" (gamemod_getmodename (getalias $gamemod_c_serverinfo)) " \" \"alias gamemod_gui_serverlist_searchterm [];" $gamemod_c_command " (getalias " $gamemod_c_serverinfo ")\" 0;");
				];
			] [
		
			if (=s $gamemod_c_col "desc") [
				if (= $gamemod_c_i 0) [ gamemod_append gamemod_c_guicode "guitext \"?*GREY()description ?*CLEAR()\" 0;"; ];
				gamemod_loopfromto $gamemod_c_list $gamemod_c_i $gamemod_c_max_i gamemod_c_serverinfo [
					gamemod_append gamemod_c_guicode (concatword "guibutton \"" (gamemod_getdesc (getalias $gamemod_c_serverinfo)) " \" \"alias gamemod_gui_serverlist_searchterm [];" $gamemod_c_command " (getalias " $gamemod_c_serverinfo ")\" 0;");
				];
			];
			
			]; // if (=s $gamemod_c_col "players")
			]; // if (=s $gamemod_c_col "mastermode")
			]; // if (=s $gamemod_c_col "map")
			]; // if (=s $gamemod_c_col "mode")
			
			gamemod_append gamemod_c_guicode "];"; // guilist [
		];
		
		gamemod_append gamemod_c_guicode "];"; // guilist [
		
		alias gamemod_c_i (+ $gamemod_c_max_i 1);
		alias gamemod_c_pagefill 0;
	];
	
	alias gamemod_c_list "";
	alias gamemod_c_listlen "";
	alias gamemod_c_i "";
	alias gamemod_c_max_i "";
	alias gamemod_c_pagefill "";
	alias gamemod_c_linesperpage "";
	alias gamemod_c_command "";
	alias gamemod_c_tabnum "";
	
	result (gamemod_resultalias gamemod_c_guicode);
];

gamemod_showgui_serverlist = [
	newgui gamemod_gui_serverlist (gamemod_getguicode_serverlist "players mastermode mode map desc" "gamemod_showservergui" "gamemod_gui_serverlist" "gamemod server list") "gamemod server list";
	if (! $arg1) [ showgui gamemod_gui_serverlist; ];
];

gamemod_showservergui = [
	?*IFGUIDEBUG(TILEOL echo (concat "gamemod_showservergui:" $arg1);)
	alias gamemod_c_gui_name (gamemod_getguiname $arg1);
	newgui $gamemod_c_gui_name (gamemod_makeservergui $arg1) (gamemod_getdesc $arg1);
	showgui $gamemod_c_gui_name;
	alias gamemod_c_gui_name "";
];

gamemod_showservergui_ip_port = [
	gamemod_showservergui (gamemod_find_serverinfo $arg1 $arg2);
];

gamemod_updateservergui = [
	newgui (gamemod_getguiname $arg1) (gamemod_makeservergui $arg1) (gamemod_getdesc $arg1);
];

gamemod_updateservergui_ip_port = [
	gamemod_updateservergui (gamemod_find_serverinfo $arg1 $arg2);
];

gamemod_search_player = [
	alias gamemod_c_search (gamemod_tolowercase $arg1);
	alias gamemod_c_foundplayers "";
	looplist gamemod_c_serverinfovar (getalias gamemod_list_servers) [
		alias gamemod_c_playerlist (gamemod_getplayerlist (getalias $gamemod_c_serverinfovar));
		looplist gamemod_c_playerinfo $gamemod_c_playerlist [
			alias gamemod_c_playername (gamemod_getplayername $gamemod_c_playerinfo);
			if (!= (strstr (gamemod_tolowercase $gamemod_c_playername) $gamemod_c_search) -1) [
				gamemod_append gamemod_c_foundplayers (concatword "\"" $gamemod_c_playername " " $gamemod_c_serverinfovar "\"");
			];
		];
	];
	alias gamemod_c_search "";
	alias gamemod_c_playername "";
	result (gamemod_resultalias gamemod_c_foundplayers);
];

gamemod_search_server = [
	alias gamemod_c_search (gamemod_tolowercase (strreplace $arg1 " " ""));
	alias gamemod_c_foundservers "";
	looplist gamemod_c_serverinfovar (getalias gamemod_list_servers) [
		alias gamemod_c_serverdesc (gamemod_tolowercase (strreplace (gamemod_getdesc (getalias $gamemod_c_serverinfovar)) " " ""));
		if (!= (strstr $gamemod_c_serverdesc $gamemod_c_search) -1) [
			gamemod_append gamemod_c_foundservers $gamemod_c_serverinfovar;
		];
	];
	alias gamemod_c_search "";
	alias gamemod_c_serverdesc "";
	result (gamemod_resultalias gamemod_c_foundservers);
];

?*IFGUIDEBUG()

gamemod_showgui_extsearch = [
	showgui gamemod_gui_extsearch;
];

?*IFGUIDEBUG()

gamemod_gui_searchupdate = [
	alias gamemod_gui_searchfield (gamemod_safestr (getalias gamemod_gui_searchfield));

	gamemod_guicode_extsearch = [
		guialign 0 [
			guistayopen [
				guibutton "?*PRIMCOLOR()>?*CLEAR()refresh" "updatefrommaster" 0;
			];
			guibar;
			guitext (concatword "?*WHITE()search for?*CLEAR() ?*PRIMCOLOR()" (getalias gamemod_gui_searchfield) "?*CLEAR()") 0;
			guibar;
			guifield gamemod_gui_searchfield 15 [gamemod_gui_searchupdate];
		];
		guibar;
	];
	alias gamemod_guicode_search "guialign 0 [guilist [";
	
	gamemod_i;
	gamemod_a gamemod_c_switched 0;
	gamemod_a gamemod_c_playerlist (gamemod_search_player (getalias gamemod_gui_searchfield));
	gamemod_a gamemod_c_serverlist (gamemod_search_server (getalias gamemod_gui_searchfield));
	gamemod_a gamemod_c_playerllen (min (listlen $gamemod_c_playerlist) (div (getalias gamemod_cfg_maxsearchelements) 2));
	gamemod_a gamemod_c_serverllen (min (listlen $gamemod_c_serverlist) (- (getalias gamemod_cfg_maxsearchelements) $gamemod_c_playerllen));
	gamemod_a gamemod_c_totallen (+ $gamemod_c_serverllen $gamemod_c_playerllen);
	gamemod_a gamemod_c_i 0;
	gamemod_a gamemod_c_results 0;
	gamemod_a gamemod_c_n 2;
	gamemod_a gamemod_c_tabnum 1;
	
	gamemod_a gamemod_c_serverinfovar;
	gamemod_a gamemod_c_serverinfo;
	gamemod_a gamemod_c_desc;
	
	looplist gamemod_c_serverinfovar $gamemod_c_serverlist [
		alias gamemod_c_desc (strreplace (gamemod_getdesc (getalias $gamemod_c_serverinfovar)) "  " " ");
		if (< $gamemod_c_i $gamemod_c_serverllen) [
			if (&& (>= $gamemod_c_results (div (+ $gamemod_c_totallen 1) 2)) (! $gamemod_c_switched)) [
				alias gamemod_c_switched 1;
				gamemod_append gamemod_guicode_search "]; guibar; guilist [";
			];
			gamemod_append gamemod_guicode_search (concatword "guibutton \"" $gamemod_c_desc "\" \"gamemod_showservergui (getalias " $gamemod_c_serverinfovar ")\" 0;");
			alias gamemod_c_i (+ $gamemod_c_i 1);
			alias gamemod_c_results (+ $gamemod_c_results 1);
		];
		alias gamemod_guicode_extsearch (concatword (getalias gamemod_guicode_extsearch) "guialign 0 [guibutton \"" $gamemod_c_desc "\" \"gamemod_showservergui (getalias " $gamemod_c_serverinfovar ")\" 0;];");
		alias gamemod_c_n (+ $gamemod_c_n 1);
		if (> $gamemod_c_n $guiautotab) [
			alias gamemod_c_tabnum (+ $gamemod_c_tabnum 1);
			alias gamemod_guicode_extsearch (concatword (getalias gamemod_guicode_extsearch) "guitab " $gamemod_c_tabnum ";");
			alias gamemod_c_n 0;
		];
	];
	
	alias gamemod_c_i 0;
	looplist gamemod_c_a $gamemod_c_playerlist [
		alias gamemod_c_serverinfovar (at $gamemod_c_a 1);
		alias gamemod_c_serverinfo (getalias $gamemod_c_serverinfovar);
		alias gamemod_c_desc (strreplace (gamemod_getdesc $gamemod_c_serverinfo) "  " " ");
		if (< $gamemod_c_i $gamemod_c_playerllen) [
			if (&& (>= $gamemod_c_results (div (+ $gamemod_c_totallen 1) 2)) (! $gamemod_c_switched)) [
				alias gamemod_c_switched 1;
				gamemod_append gamemod_guicode_search "]; guibar; guilist [";
			];
			gamemod_append gamemod_guicode_search (concatword "guibutton \"?*PRIMCOLOR()" (at $gamemod_c_a 0) "?*CLEAR() ?*GREY()on?*CLEAR() " $gamemod_c_desc "\" \"gamemod_showservergui (getalias " $gamemod_c_serverinfovar ")\" 0;");
			alias gamemod_c_i (+ $gamemod_c_i 1);
			alias gamemod_c_results (+ $gamemod_c_results 1);
		];
		alias gamemod_guicode_extsearch (concatword (getalias gamemod_guicode_extsearch) "guialign 0 [guibutton \"?*PRIMCOLOR()" (at $gamemod_c_a 0) "?*CLEAR() ?*GREY()on?*CLEAR() " $gamemod_c_desc "\" \"gamemod_showservergui (getalias " $gamemod_c_serverinfovar ")\" 0;];");
		alias gamemod_c_n (+ $gamemod_c_n 1);
		if (> $gamemod_c_n $guiautotab) [
			alias gamemod_c_tabnum (+ $gamemod_c_tabnum 1);
			alias gamemod_guicode_extsearch (concatword (getalias gamemod_guicode_extsearch) "guitab " $gamemod_c_tabnum ";");
			alias gamemod_c_n 0;
		];
	];
	
	gamemod_append gamemod_guicode_search "];];"; // guialign 0 [guilist [
	gamemod_append gamemod_guicode_search "guialign 0 [";
	if (> (+ (listlen $gamemod_c_serverlist) (listlen $gamemod_c_playerlist)) $gamemod_c_totallen) [
		gamemod_append gamemod_guicode_search (concatword "guibutton \"?*PRIMCOLOR()" (- (+ (listlen $gamemod_c_serverlist) (listlen $gamemod_c_playerlist)) (+ $gamemod_c_serverllen $gamemod_c_playerllen)) "?*CLEAR() more...\" \"gamemod_showgui_extsearch\" 0; guibar;");
	];
	gamemod_append gamemod_guicode_search [guistayopen [guibutton "?*PRIMCOLOR()>?*CLEAR()close" "gamemod_gui_searchfield_enable = 0;" 0;];];
	gamemod_append gamemod_guicode_search "];"; // guialign 0 [
	
	newgui gamemod_gui_extsearch $gamemod_guicode_extsearch "search";
	
	gamemod_c;
];

?*IFGUIDEBUG()

gamemod_formatcc = [
	gamemod_i;
	gamemod_a gamemod_c_cc $arg1;
	gamemod_a gamemod_c_mcc $arg2;
	gamemod_a gamemod_c_ecc (* $gamemod_c_cc 100);
	gamemod_a gamemod_c_perc (div $gamemod_c_ecc $gamemod_c_mcc);
	gamemod_a gamemod_c_color "?*ORANGE()";
	gamemod_a gamemod_c_counts (concatword $gamemod_c_cc "/" $gamemod_c_mcc);
	if (|| (< $gamemod_c_cc 0) (=s $gamemod_c_cc "") (=s $gamemod_c_mcc "")) [
		gamemod_c_color = "?*GREY()";
		gamemod_c_counts = "-";
	] [
		if (>= $gamemod_c_perc 100) [ gamemod_c_color = "?*RED()"; ];
	];
	result (gamemod_c (concatword $gamemod_c_color $gamemod_c_counts "?*CLEAR()"));
];

gamemod_getclientcounts = [ // get client counts string, args: ip, port // TODO get rid of this
	gamemod_i;
	gamemod_a gamemod_c_varext (concatword $arg1 "_" $arg2);
	gamemod_a gamemod_c_servinfo (getalias (concatword "gamemod_serverinfo_" $gamemod_c_varext));
	gamemod_a gamemod_c_cc (at $gamemod_c_servinfo 2);
	gamemod_a gamemod_c_mcc (at $gamemod_c_servinfo 3);
	result (gamemod_c (gamemod_formatcc $gamemod_c_cc $gamemod_c_mcc));
];

gamemod_getcc = [ // get client counts string (like above), args: serverinfo (content)
	result (gamemod_formatcc (gamemod_getclients $arg1) (gamemod_getmaxclients $arg1));
];

?*IFGUIDEBUG()

gamemod_list_del_index = [
	gamemod_i;
	gamemod_a gamemod_c_newlist "";
	gamemod_a gamemod_c_i 0;
	looplist gamemod_c_listelement (getalias $arg1) [
		if (!= $gamemod_c_i $arg2) [ gamemod_append gamemod_c_newlist $gamemod_c_listelement; ];
		alias gamemod_c_i (+ $gamemod_c_i 1);
	];
	alias $arg1 $gamemod_c_newlist;
	gamemod_c;
];

gamemod_list_insert = [ // args: list-varname item before-index
	gamemod_i;
	gamemod_a gamemod_c_newlist "";
	gamemod_a gamemod_c_i 0;
	gamemod_a gamemod_c_inserted 0;
	looplist gamemod_c_listelement (getalias $arg1) [
		if (= $gamemod_c_i $arg3) [
			gamemod_append gamemod_c_newlist $arg2;
			alias gamemod_c_inserted 1;
		];
		gamemod_append gamemod_c_newlist $gamemod_c_listelement;
		alias gamemod_c_i (+ $gamemod_c_i 1);
	];
	if (! $gamemod_c_inserted) [
		gamemod_append gamemod_c_newlist $arg2;
	];
	alias $arg1 $gamemod_c_newlist;
	gamemod_c;
];

gamemod_getvarname = [
	alias gamemod_c_newindex 0;
	while [!=s (getalias (concatword $arg1 $gamemod_c_newindex)) ""] [ alias gamemod_c_newindex (+ $gamemod_c_newindex 1); ];
	result (gamemod_rc (concatword $arg1 $gamemod_c_newindex) gamemod_c_newindex);
];

gamemod_quickconnect_clear = [
	while [listlen (getalias gamemod_cfg_quickconnect)] [
		gamemod_quickconnect_del_row 0;
	];
	?*IFGUIDEBUG(TILEOL echo "qc clear: ";)
	alias gamemod_cfg_quickconnect "";
];

gamemod_quickconnect_add_row = [
	?*IFGUIDEBUG(TILEOL echo (concat "qc add row:" $arg1);)
	alias gamemod_c_varname (gamemod_getvarname "gamemod_cfg_quickconnect_row_");
	gamemod_append gamemod_cfg_quickconnect $gamemod_c_varname;
	alias $gamemod_c_varname "0";
	alias gamemod_c_varname "";
	result (- (listlen (getalias gamemod_cfg_quickconnect)) 1);
];

gamemod_quickconnect_del_row = [
	looplist gamemod_c_curblockvarname (getalias (at (getalias gamemod_cfg_quickconnect) $arg1)) [
		//echo (concatword "> clearing " $gamemod_c_curblockvarname);
		gamemod_quickconnect_del_block -1 $gamemod_c_curblockvarname; // get block and its contents cleared
	];
	alias (at (getalias gamemod_cfg_quickconnect) $arg1) "";
	gamemod_list_del_index gamemod_cfg_quickconnect $arg1;
	?*IFGUIDEBUG(TILEOL echo (concatword "qc del row: " $arg1);)
];

gamemod_quickconnect_move_row = [
	alias gamemod_c_quickconnect_var gamemod_cfg_quickconnect;
	alias gamemod_c_row_var (at (getalias $gamemod_c_quickconnect_var) $arg1);
	gamemod_list_del_index $gamemod_c_quickconnect_var $arg1;
	gamemod_list_insert $gamemod_c_quickconnect_var $gamemod_c_row_var (+ $arg1 $arg2);
	alias gamemod_c_quickconnect_var "";
	alias gamemod_c_row_var "";
];

gamemod_quickconnect_add_block = [
	alias gamemod_c_varname (gamemod_getvarname "gamemod_cfg_quickconnect_block_");
	gamemod_append (at (getalias gamemod_cfg_quickconnect) $arg1) $gamemod_c_varname;
	?*IFGUIDEBUG(TILEOL echo (concatword "qc add block to row " $arg1 ": " $arg2);)
	alias $gamemod_c_varname "0";
	alias gamemod_c_varname "";
	result (- (listlen (getalias (at (getalias gamemod_cfg_quickconnect) $arg1))) 1);
];

gamemod_quickconnect_add_block_with = [ // $row_index ["Name 1" "Name 2"] ["ip1 port1" "ip2 port2"];
	gamemod_i;
	gamemod_a gamemod_c_blocki (gamemod_quickconnect_add_block $arg1);
	gamemod_a gamemod_c_i 0;
	gamemod_a gamemod_c_elementvarname "";
	gamemod_a gamemod_c_ipport "";
	looplist gamemod_c_name $arg2 [
		alias gamemod_c_elementvarname (gamemod_quickconnect_add_element $arg1 $gamemod_c_blocki);
		alias gamemod_c_ipport (at $arg3 $gamemod_c_i);
		gamemod_quickconnect_set_element_name_now $gamemod_c_elementvarname $gamemod_c_name;
		gamemod_quickconnect_set_element_server_ip_port $gamemod_c_elementvarname (at $gamemod_c_ipport 0) (at $gamemod_c_ipport 1);
		alias gamemod_c_i (+ $gamemod_c_i 1);
	];
	gamemod_c;
];

gamemod_quickconnect_del_block = [
	//echo (concatword "delblock called");
	if (= $arg1 -1) [ //clear this list and it's contents
		//echo (concatword "delblock: loop starting");
		looplist gamemod_c_curelementvarname (getalias $arg2) [
			//echo (concatword "-> clearing " $gamemod_c_curelementvarname);
			gamemod_quickconnect_del_element -1 $gamemod_c_curelementvarname;
		];
		alias $arg2 "";
	] [
		//alias (at (getalias (at (getalias gamemod_cfg_quickconnect) $arg1)) $arg2) "";
		gamemod_quickconnect_del_block -1 (at (getalias (at (getalias gamemod_cfg_quickconnect) $arg1)) $arg2);
		gamemod_list_del_index (at (getalias gamemod_cfg_quickconnect) $arg1) $arg2;
		?*IFGUIDEBUG(TILEOL echo (concatword "qc del block in row " $arg1 ": " $arg2);)
	];
];

gamemod_quickconnect_move_block = [ // args: row-index block-index moveval
	alias gamemod_c_row_var (at (getalias gamemod_cfg_quickconnect) $arg1);
	alias gamemod_c_block_var (at (getalias $gamemod_c_row_var) $arg2);
	gamemod_list_del_index $gamemod_c_row_var $arg2;
	gamemod_list_insert $gamemod_c_row_var $gamemod_c_block_var (+ $arg2 $arg3);
	alias gamemod_c_row_var "";
	alias gamemod_c_block_var "";
];

gamemod_quickconnect_move_block_interrow = [
	alias gamemod_c_row_var_from (at (getalias gamemod_cfg_quickconnect) $arg1);
	alias gamemod_c_row_var_to (at (getalias gamemod_cfg_quickconnect) (+ $arg1 $arg3));
	
	alias gamemod_c_block_var (at (getalias $gamemod_c_row_var_from) $arg2);
	gamemod_list_del_index $gamemod_c_row_var_from $arg2;
	gamemod_list_insert $gamemod_c_row_var_to $gamemod_c_block_var $arg2;
	
	alias gamemod_c_row_var_from "";
	alias gamemod_c_row_var_to "";
	alias gamemod_c_block_var "";
];

gamemod_quickconnect_add_element = [
	alias gamemod_c_varname (gamemod_getvarname "gamemod_cfg_quickconnect_element_");
	gamemod_append (at (getalias (at (getalias gamemod_cfg_quickconnect) $arg1)) $arg2) $gamemod_c_varname;
	?*IFGUIDEBUG(TILEOL echo (concatword "qc add element to row " $arg1 " and block " $arg2 ": " $arg3 " -> " $gamemod_c_varname);)
	alias gamemod_c_elementname_var (concatword $gamemod_c_varname "_name");
	alias $gamemod_c_elementname_var "";
	alias $gamemod_c_varname $gamemod_c_elementname_var;
	alias gamemod_c_elementname_var "";
	result (gamemod_resultalias gamemod_c_varname);
];

gamemod_quickconnect_set_element_server_ip_port = [
	alias $arg1 (concat (at (getalias $arg1) 0) $arg2 $arg3);
];

gamemod_quickconnect_set_element_server = [
	//echo (concat "set server of" $arg1 "to" $arg2 $arg3);
	alias gamemod_c_serverinfo $arg2;
	gamemod_quickconnect_set_element_server_ip_port $arg1 (gamemod_getip $gamemod_c_serverinfo) (gamemod_getport $gamemod_c_serverinfo);
	if (! (getalias (at (getalias $arg1) 0))) [
		gamemod_quickconnect_set_element_name_now $arg1 (gamemod_getdesc $gamemod_c_serverinfo);
	];
	gamemod_gui_quickconnectupdate 1;
	gamemod_showgui_quickconnect_mod_element $arg1 0;
	alias gamemod_c_serverinfo "";
];

gamemod_quickconnect_set_element_name_now = [
	//alias $arg1 (concat (strreplace (strreplace $arg2 " " "") "^"" "") (at (getalias $arg1) 1) (at (getalias $arg1) 2));
	alias (at (getalias $arg1) 0) $arg2;
];

gamemod_quickconnect_set_element_name = [
	?*IFGUIDEBUG(TILEOL echo (concatword "setting name: " $arg1 " -> " $arg2);)
	gamemod_quickconnect_set_element_name_now $arg1 $arg2;
	gamemod_gui_quickconnectupdate 1;
	gamemod_showgui_quickconnect_mod_element $arg1 1;
];

gamemod_quickconnect_set_element_name_from_var = [
	//alias gamemod_c_s (strreplace (strreplace $gamemod_c_editelementname " " "") "^"" "");
	alias gamemod_c_s $gamemod_c_editelementname;
	if (> (strlen $gamemod_c_s) 0) [ gamemod_quickconnect_set_element_name $arg1 $gamemod_c_s; ];
];

gamemod_showgui_quickconnect_add_element = [
	
];

gamemod_showgui_quickconnect_mod_element_serverlist = [
	?*IFGUIDEBUG(TILEOL echo (concat "->>> showing server list for changing qc element, command: " $arg1);)
	newgui gamemod_gui_mod_element_serverlist (gamemod_getguicode_serverlist "players mastermode mode map desc" $arg1 "gamemod_gui_mod_element_serverlist" "select server") "select server";
	showgui gamemod_gui_mod_element_serverlist;
];

gamemod_showgui_quickconnect_mod_element = [
	gamemod_i;
	gamemod_a gamemod_c_guicode "";
	gamemod_a gamemod_c_element (getalias $arg1);
	if (>= (listlen $gamemod_c_element) 3) [
		gamemod_a gamemod_c_serverinfo (gamemod_find_serverinfo (at $gamemod_c_element 1) (at $gamemod_c_element 2));
	] [
		gamemod_a gamemod_c_serverinfo "";
	];
	
	gamemod_append gamemod_c_guicode "guilist [";

	if (>= (listlen $gamemod_c_element) 3) [
		gamemod_append gamemod_c_guicode "guilist [";
		gamemod_append gamemod_c_guicode [guialign 0 [guitext "?*WHITE()name:?*CLEAR()" 0;];];
		gamemod_append gamemod_c_guicode (concatword "guifield " (at $gamemod_c_element 0) " 15 [gamemod_gui_quickconnectupdate 1];");
		gamemod_append gamemod_c_guicode "];"; // guilist
		gamemod_append gamemod_c_guicode "guibar;";
	];
	
	gamemod_append gamemod_c_guicode "guilist [";
	if (getalias gamemod_c_serverinfo) [
		gamemod_append gamemod_c_guicode (concatword "guitext ^"?*WHITE()(?*CLEAR()?*GREY()" (gamemod_getdesc $gamemod_c_serverinfo) "?*CLEAR()?*WHITE())?*CLEAR() (?*GREY()" (gamemod_getip $gamemod_c_serverinfo) ":" (gamemod_getport $gamemod_c_serverinfo) "?*CLEAR()?*WHITE())?*CLEAR()^" 0;");
		gamemod_append gamemod_c_guicode "guibar;";
	];
	gamemod_append gamemod_c_guicode "guialign 0 [";
	gamemod_append gamemod_c_guicode (concatword "guibutton \"?*PRIMCOLOR()>?*CLEAR()select server\" \"gamemod_showgui_quickconnect_mod_element_serverlist [gamemod_quickconnect_set_element_server " $arg1 "]\" 0;");
	gamemod_append gamemod_c_guicode "];"; // guialign 0
	gamemod_append gamemod_c_guicode "];"; // guilist
	
	gamemod_append gamemod_c_guicode "];";
	
	newgui gamemod_gui_quickconnect_mod_element $gamemod_c_guicode "edit";
	if (! $arg2) [ showgui gamemod_gui_quickconnect_mod_element; ];
	gamemod_c;
];

gamemod_quickconnect_move_element = [ // args: rowindex, blockindex, elementindex, moveval (1 for down, -1 for up)
	alias gamemod_c_block_var (at (getalias (at (getalias gamemod_cfg_quickconnect) $arg1)) $arg2);
	alias gamemod_c_element_var (at (getalias $gamemod_c_block_var) $arg3);
	gamemod_list_del_index $gamemod_c_block_var $arg3;
	gamemod_list_insert $gamemod_c_block_var $gamemod_c_element_var (+ $arg3 $arg4);
	alias gamemod_c_block_var "";
	alias gamemod_c_element_var "";
];

gamemod_quickconnect_move_element_interblock = [ // args: rowindex, blockindex, elementindex, moveval (1 for right, -1 for left)
	alias gamemod_c_block_var_from (at (getalias (at (getalias gamemod_cfg_quickconnect) $arg1)) $arg2);
	alias gamemod_c_block_var_to (at (getalias (at (getalias gamemod_cfg_quickconnect) $arg1)) (+ $arg2 $arg4));
	
	alias gamemod_c_element_var (at (getalias $gamemod_c_block_var_from) $arg3);
	gamemod_list_del_index $gamemod_c_block_var_from $arg3;
	gamemod_list_insert $gamemod_c_block_var_to $gamemod_c_element_var $arg3;
	
	alias gamemod_c_block_var_from "";
	alias gamemod_c_block_var_to "";
	alias gamemod_c_element_var "";
];

gamemod_quickconnect_del_element = [
	if (= $arg1 -1) [ //clear this ($arg2)
		alias $arg2 "";
	] [
		gamemod_quickconnect_del_element -1 (at (getalias (at (getalias (at (getalias gamemod_cfg_quickconnect) $arg1)) $arg2)) $arg3);
		gamemod_list_del_index (at (getalias (at (getalias gamemod_cfg_quickconnect) $arg1)) $arg2) $arg3;
		?*IFGUIDEBUG(TILEOL echo (concatword "qc del element in row " $arg1 " and block " $arg2 ": " $arg3);)
	];
];

gamemod_getquickconnectguicode = [
	alias gamemod_c_list (getalias gamemod_cfg_quickconnect);
	alias gamemod_c_guicode "";
	alias gamemod_c_rowi 0;
	alias gamemod_c_numrows (listlen $gamemod_c_list);
	if $arg1 [ gamemod_append gamemod_c_guicode "guistayopen ["; ];
	looplist gamemod_c_qcrow $gamemod_c_list [
		if (> $gamemod_c_rowi 0) [ gamemod_append gamemod_c_guicode "guibar;"; ];
		gamemod_append gamemod_c_guicode "guialign 0 [";
		
		alias gamemod_c_blocki 0;
		alias gamemod_c_block_number 0;
		alias gamemod_c_numblocks (listlen (getalias $gamemod_c_qcrow));
		alias gamemod_c_guibar_block 0;
		looplist gamemod_c_qcblock (getalias $gamemod_c_qcrow) [
			if (!=s $gamemod_c_qcblock "0") [
				if ($gamemod_c_guibar_block) [ gamemod_append gamemod_c_guicode "guibar;"; ];
			
				if $arg1 [
					gamemod_append gamemod_c_guicode "guilist [guilist [";
				];
				
				alias gamemod_c_numelements (listlen (getalias $gamemod_c_qcblock));
			
				gamemod_append gamemod_c_guicode "guilist [";
				looplist gamemod_c_qcelementname (getalias $gamemod_c_qcblock) [
					alias gamemod_c_qcelement (getalias $gamemod_c_qcelementname);
					if (!=s $gamemod_c_qcelementname "0") [
						if $arg1 [
							if (!=s (getalias (at $gamemod_c_qcelement 0)) "") [
								gamemod_append gamemod_c_guicode (concatword "guitext ^"?*WHITE()" (strreplace (getalias (at $gamemod_c_qcelement 0)) "^"" "^^^"") "?*CLEAR()^" 0;");
							] [
								gamemod_append gamemod_c_guicode [guitext "?*GREY()unset?*CLEAR()" 0;];
							];
						] [
							if (>= (listlen $gamemod_c_qcelement) 3) [
								alias gamemod_c_serverinfovar (gamemod_find_serverinfovar (at $gamemod_c_qcelement 1) (at $gamemod_c_qcelement 2));
								if $gamemod_c_serverinfovar [
									gamemod_append gamemod_c_guicode (concatword "guibutton ^"[?*PRIMCOLOR()i?*CLEAR()] ^" ^"gamemod_showservergui (getalias " $gamemod_c_serverinfovar ")^" 0;");
								] [
									gamemod_append gamemod_c_guicode [guitext "" 0;];
								];
							] [
								gamemod_append gamemod_c_guicode [guitext "" 0;];
							];
						];
					];
				];
				gamemod_append gamemod_c_guicode "];";
			
				gamemod_append gamemod_c_guicode "guilist [";
				looplist gamemod_c_qcelementname (getalias $gamemod_c_qcblock) [
					alias gamemod_c_qcelement (getalias $gamemod_c_qcelementname);
					if (!=s $gamemod_c_qcelementname "0") [
						if (! $arg1) [
							if (!=s (getalias (at $gamemod_c_qcelement 0)) "") [
								gamemod_append gamemod_c_guicode (concatword "guibutton ^"" (strreplace (getalias (at $gamemod_c_qcelement 0)) "^"" "^^^"") "^" ^"connect " (at $gamemod_c_qcelement 1) " " (at $gamemod_c_qcelement 2) "^" 0;");
							] [
								gamemod_append gamemod_c_guicode [guitext "?*GREY()unset?*CLEAR()" 0;];
							];
						];
					];
				];
				gamemod_append gamemod_c_guicode "];";
			
				gamemod_append gamemod_c_guicode "guilist [";
				alias gamemod_c_elementi 0;
				alias gamemod_c_element_number 0;
				looplist gamemod_c_qcelementname (getalias $gamemod_c_qcblock) [
					alias gamemod_c_qcelement (getalias $gamemod_c_qcelementname);
					if (!=s $gamemod_c_qcelementname "0") [
						if $arg1 [
							?*IFGUIDEBUG(TILEOL echo (concatword "gamemod_c_qcelement = ^"" $gamemod_c_qcelement "^", gamemod_c_qcelementname = ^"" $gamemod_c_qcelementname "^"");)
							gamemod_append gamemod_c_guicode "guilist [";
							gamemod_append gamemod_c_guicode (concatword "guibutton ^" [?*PRIMCOLOR()edit?*CLEAR()]^" ^"gamemod_showgui_quickconnect_mod_element " $gamemod_c_qcelementname "^" 0;");
							gamemod_append gamemod_c_guicode (concatword "guibutton ^" [?*RED()-?*CLEAR()]^" ^"gamemod_quickconnect_del_element " $gamemod_c_rowi " " $gamemod_c_blocki " " $gamemod_c_elementi "; gamemod_gui_quickconnectupdate 1^" 0;");
							if (> $gamemod_c_element_number 0) [
								gamemod_append gamemod_c_guicode (concatword "guibutton \" [?*ORANGE()up?*CLEAR()]\" \"gamemod_quickconnect_move_element " $gamemod_c_rowi " " $gamemod_c_blocki " " $gamemod_c_elementi " -1; gamemod_gui_quickconnectupdate 1\" 0;");
							];
							if (< $gamemod_c_elementi (- $gamemod_c_numelements 1)) [
								gamemod_append gamemod_c_guicode (concatword "guibutton \" [?*ORANGE()down?*CLEAR()]\" \"gamemod_quickconnect_move_element " $gamemod_c_rowi " " $gamemod_c_blocki " " $gamemod_c_elementi " 1; gamemod_gui_quickconnectupdate 1\" 0;");
							];
							
							if (> $gamemod_c_block_number 0) [
								gamemod_append gamemod_c_guicode (concatword "guibutton \" [?*ORANGE()<?*CLEAR()]\" \"gamemod_quickconnect_move_element_interblock " $gamemod_c_rowi " " $gamemod_c_blocki " " $gamemod_c_elementi " -1; gamemod_gui_quickconnectupdate 1\" 0;");
							];
							if (< $gamemod_c_blocki (- $gamemod_c_numblocks 1)) [
								gamemod_append gamemod_c_guicode (concatword "guibutton \" [?*ORANGE()>?*CLEAR()]\" \"gamemod_quickconnect_move_element_interblock " $gamemod_c_rowi " " $gamemod_c_blocki " " $gamemod_c_elementi " 1; gamemod_gui_quickconnectupdate 1\" 0;");
							];
							gamemod_append gamemod_c_guicode "];";
						] [
							if (>= (listlen $gamemod_c_qcelement) 3) [
								alias gamemod_c_serverinfo (gamemod_find_serverinfo (at $gamemod_c_qcelement 1) (at $gamemod_c_qcelement 2));
								if $gamemod_c_serverinfo [
									gamemod_append gamemod_c_guicode (concatword "guitext ^" ?*WHITE()[?*CLEAR()" (gamemod_getcc $gamemod_c_serverinfo) "?*WHITE()]?*CLEAR()^" 0;");
								] [
									gamemod_append gamemod_c_guicode [guitext " ?*GREY()[not found]?*CLEAR()" 0;];
								];
							] [
								gamemod_append gamemod_c_guicode [guitext "" 0;];
							];
						];
						alias gamemod_c_element_number (+ $gamemod_c_element_number 1);
					];
					alias gamemod_c_elementi (+ $gamemod_c_elementi 1);
				];
				gamemod_append gamemod_c_guicode "];";
			
				if $arg1 [
					gamemod_append gamemod_c_guicode "]; guialign 0 [";
					gamemod_append gamemod_c_guicode (concatword "guibutton ^"[?*GREEN()+?*CLEAR()]^" ^"gamemod_quickconnect_add_element " $gamemod_c_rowi " " $gamemod_c_blocki " " $gamemod_c_elementi "; gamemod_gui_quickconnectupdate 1^" 0;");
					gamemod_append gamemod_c_guicode "]; guibar; guialign 0 [";
					gamemod_append gamemod_c_guicode (concatword "guibutton ^"[?*RED()-?*CLEAR() block]^" ^"gamemod_quickconnect_del_block " $gamemod_c_rowi " " $gamemod_c_blocki "; gamemod_gui_quickconnectupdate 1^" 0;");
					if (> $gamemod_c_block_number 0) [
						gamemod_append gamemod_c_guicode (concatword "guibutton \" [?*PRIMCOLOR()<?*CLEAR()]\" \"gamemod_quickconnect_move_block " $gamemod_c_rowi " " $gamemod_c_blocki " -1; gamemod_gui_quickconnectupdate 1\" 0;");
					];
					if (< $gamemod_c_blocki (- $gamemod_c_numblocks 1)) [
						gamemod_append gamemod_c_guicode (concatword "guibutton \" [?*PRIMCOLOR()>?*CLEAR()]\" \"gamemod_quickconnect_move_block " $gamemod_c_rowi " " $gamemod_c_blocki " 1; gamemod_gui_quickconnectupdate 1\" 0;");
					];
					if (> $gamemod_c_rowi 0) [
						gamemod_append gamemod_c_guicode (concatword "guibutton \" [?*PRIMCOLOR()up?*CLEAR()]\" \"gamemod_quickconnect_move_block_interrow " $gamemod_c_rowi " " $gamemod_c_blocki " -1; gamemod_gui_quickconnectupdate 1\" 0;");
					];
					if (< $gamemod_c_rowi (- $gamemod_c_numrows 1)) [
						gamemod_append gamemod_c_guicode (concatword "guibutton \" [?*PRIMCOLOR()down?*CLEAR()]\" \"gamemod_quickconnect_move_block_interrow " $gamemod_c_rowi " " $gamemod_c_blocki " 1; gamemod_gui_quickconnectupdate 1\" 0;");
					];
					gamemod_append gamemod_c_guicode "];];";
				];
				
				alias gamemod_c_guibar_block 1;
				alias gamemod_c_block_number (+ $gamemod_c_block_number 1);
			]; //if (!=s $gamemod_c_qcblock "0")
			alias gamemod_c_blocki (+ $gamemod_c_blocki 1);
		];
		
		if $arg1 [
			if (> $gamemod_c_blocki 0) [ gamemod_append gamemod_c_guicode "guibar;"; ];
			gamemod_append gamemod_c_guicode (concatword "guialign 0 [guibutton ^"[?*GREEN()+?*CLEAR() block]^" ^"gamemod_quickconnect_add_block " $gamemod_c_rowi " " $gamemod_c_blocki "; gamemod_gui_quickconnectupdate 1^" 0;];");
			gamemod_append gamemod_c_guicode "guibar;";
			gamemod_append gamemod_c_guicode (concatword "guialign 0 [guibutton ^"[?*RED()-?*CLEAR() row]^" ^"gamemod_quickconnect_del_row " $gamemod_c_rowi "; gamemod_gui_quickconnectupdate 1^" 0;];");
			if (> $gamemod_c_rowi 0) [
				gamemod_append gamemod_c_guicode (concatword "guialign 0 [guibutton \" [?*PRIMCOLOR()up?*CLEAR()]\" \"gamemod_quickconnect_move_row " $gamemod_c_rowi " -1; gamemod_gui_quickconnectupdate 1\" 0;];");
			];
			if (< $gamemod_c_rowi (- $gamemod_c_numrows 1)) [
				gamemod_append gamemod_c_guicode (concatword "guialign 0 [guibutton \" [?*PRIMCOLOR()down?*CLEAR()]\" \"gamemod_quickconnect_move_row " $gamemod_c_rowi " 1; gamemod_gui_quickconnectupdate 1\" 0;];");
			];
		];
		
		gamemod_append gamemod_c_guicode "];";
		
		alias gamemod_c_rowi (+ $gamemod_c_rowi 1);
	];
	
	if $arg1 [
		if (> $gamemod_c_rowi 0) [ gamemod_append gamemod_c_guicode "guibar;"; ];
		gamemod_append gamemod_c_guicode (concatword "guialign 0 [guibutton ^"[?*GREEN()+?*CLEAR() row]^" ^"gamemod_quickconnect_add_row " $gamemod_c_rowi "; gamemod_gui_quickconnectupdate 1^" 0;];");
		gamemod_append gamemod_c_guicode "];";
	];
	
	alias gamemod_c_list "";
	alias gamemod_c_rowi "";
	alias gamemod_c_numrows "";
	alias gamemod_c_blocki "";
	alias gamemod_c_block_number "";
	alias gamemod_c_elementi "";
	alias gamemod_c_element_number "";
	alias gamemod_c_numblocks "";
	alias gamemod_c_guibar_block "";
	alias gamemod_c_numelements "";
	alias gamemod_c_qcelement "";
	alias gamemod_c_serverinfovar "";
	alias gamemod_c_serverinfo "";
	
	result (gamemod_resultalias gamemod_c_guicode);
];

gamemod_gui_quickconnectupdate = [
	if $arg1 [ gamemod_gui_quickconnecteditupdate; ];
	alias gamemod_guicode_quickconnect (gamemod_getquickconnectguicode 0);
];

gamemod_gui_quickconnecteditupdate = [
	alias gamemod_guicode_quickconnectedit (gamemod_getquickconnectguicode 1);
];

?*IFGUIDEBUG()

gamemod_filter_open_switch = [
	alias gamemod_gui_servers_filter_open $arg1;
	if $arg1 [ // enable filter
		clearservers;
		looplist gamemod_c_serverinfovar (getalias gamemod_list_servers) [
			alias gamemod_c_serverinfo (getalias $gamemod_c_serverinfovar);
			if (= (gamemod_getmastermode $gamemod_c_serverinfo) 0) [
				addserver (gamemod_getip $gamemod_c_serverinfo) (gamemod_getport $gamemod_c_serverinfo);
			];
		];
	] [
		looplist gamemod_c_serverinfovar (getalias gamemod_list_servers) [
			alias gamemod_c_serverinfo (getalias $gamemod_c_serverinfovar);
			addserver (gamemod_getip $gamemod_c_serverinfo) (gamemod_getport $gamemod_c_serverinfo);
		];
	];
	alias gamemod_c_serverinfo "";
];

?*IFGUIDEBUG()

newgui gamemod_gui_settings [
	guistayopen [
		if (getalias gamemod_c_reset_settings) [
			guilist [
				guitext "?*GREY()>reset all settings?*CLEAR()" 0;
				guibar;
				guibutton "?*PRIMCOLOR()>?*CLEAR()confirm (this will also clear quickconnect)" [
					gamemod_reset;
					gamemod_c_reset_settings = 0;
				] 0;
			];
		] [
			guibutton "?*PRIMCOLOR()>?*CLEAR()reset all settings" [
				gamemod_c_reset_settings = 1;
				sleep 5000 [gamemod_c_reset_settings = 0];
			] 0;
		];
	];
	guibar;
	guibutton "?*PRIMCOLOR()>?*CLEAR()quickconnect configuration" [showgui gamemod_gui_quickconnect_config] 0;
	guibar;
	guilist [
		guilist [
			guitext "?*WHITE()show server browser on startup (works only if gamemod is installed)?*CLEAR()" 0;
			guitext (concatword "?*WHITE()blinking refresh button if refresh is more than ?*PRIMCOLOR()" (div (getalias gamemod_cfg_gui_servers_refresh_button_blink_after) 1000) "?*CLEAR() seconds ago?*CLEAR()") 0;
			guitext (concatword (? (getalias gamemod_cfg_gui_servers_refresh_button_blink) "?*WHITE()" "?*GREY()") "-> seconds since last refresh after which the refresh button starts to blink?*CLEAR()") 0;
			guitext (concatword (? (getalias gamemod_cfg_gui_servers_refresh_button_blink) "?*WHITE()" "?*GREY()") "-> refresh button blink speed in milliseconds?*CLEAR()") 0;
			guitext (concatword "?*WHITE()automatically refresh if refresh is more than ?*PRIMCOLOR()" (div (getalias gamemod_cfg_gui_servers_autorefresh_after) 1000) "?*CLEAR() seconds ago?*CLEAR()") 0;
			guitext (concatword (? (getalias gamemod_cfg_gui_servers_autorefresh) "?*WHITE()" "?*GREY()") "-> seconds that have to pass before an auto-refresh?*CLEAR()") 0;
			guitext "?*WHITE()number of search results displayed in server browser?*CLEAR()" 0;
			guitext "?*WHITE()hide the 'open only' button in server browser?*CLEAR()" 0;
			guitext "?*WHITE()show client counts in server browser?*CLEAR()" 0;
			guitext "?*WHITE()show server counts in server browser?*CLEAR()" 0;
		];
		guibar;
		guilist [
			guicheckbox "" gamemod_cfg_showgui_servers_on_launch;
			
			guicheckbox "" gamemod_cfg_gui_servers_refresh_button_blink;
			guilist [
				guitext (concatword "?*PRIMCOLOR()" (div (getalias gamemod_cfg_gui_servers_refresh_button_blink_after) 1000) "?*CLEAR()") 0;
				guistayopen [
					guibutton " [?*GREEN()+?*CLEAR()]" [
						alias gamemod_cfg_gui_servers_refresh_button_blink_after (* (+ (div (getalias gamemod_cfg_gui_servers_refresh_button_blink_after) 1000) 1) 1000);
					] 0;
					guibutton " [?*RED()-?*CLEAR()]" [
						if (> (getalias gamemod_cfg_gui_servers_refresh_button_blink_after) 1000) [
							alias gamemod_cfg_gui_servers_refresh_button_blink_after (* (- (div (getalias gamemod_cfg_gui_servers_refresh_button_blink_after) 1000) 1) 1000);
						] [
							alias gamemod_cfg_gui_servers_refresh_button_blink_after 0;
						];
					] 0;
				];
			];
			guilist [
				guifield gamemod_cfg_gui_servers_refresh_button_blink_interval 5;
				guistayopen [
					guibutton " [?*GREEN()+?*CLEAR()]" [
						alias gamemod_cfg_gui_servers_refresh_button_blink_interval (* (+ (div (getalias gamemod_cfg_gui_servers_refresh_button_blink_interval) 50) 1) 50);
					] 0;
					guibutton " [?*RED()-?*CLEAR()]" [
						if (> (getalias gamemod_cfg_gui_servers_refresh_button_blink_interval) 50) [
							alias gamemod_cfg_gui_servers_refresh_button_blink_interval (* (- (div (getalias gamemod_cfg_gui_servers_refresh_button_blink_interval) 50) 1) 50);
						] [
							alias gamemod_cfg_gui_servers_refresh_button_blink_interval 0;
						];
					] 0;
				];
			];
			
			guicheckbox "" gamemod_cfg_gui_servers_autorefresh;
			guilist [
				guitext (concatword "?*PRIMCOLOR()" (div (getalias gamemod_cfg_gui_servers_autorefresh_after) 1000) "?*CLEAR()") 0;
				guistayopen [
					guibutton " [?*GREEN()+?*CLEAR()]" [
						alias gamemod_cfg_gui_servers_autorefresh_after (* (+ (div (getalias gamemod_cfg_gui_servers_autorefresh_after) 1000) 1) 1000);
					] 0;
					guibutton " [?*RED()-?*CLEAR()]" [
						if (> (getalias gamemod_cfg_gui_servers_autorefresh_after) 1000) [
							alias gamemod_cfg_gui_servers_autorefresh_after (* (- (div (getalias gamemod_cfg_gui_servers_autorefresh_after) 1000) 1) 1000);
						] [
							alias gamemod_cfg_gui_servers_autorefresh_after 0;
						];
					] 0;
				];
			];
			
			guilist [
				guitext (concatword "?*PRIMCOLOR()" (getalias gamemod_cfg_maxsearchelements) "?*CLEAR()") 0;
				guistayopen [
					guibutton " [?*GREEN()+?*CLEAR()]" [
						alias gamemod_cfg_maxsearchelements (+ (getalias gamemod_cfg_maxsearchelements) 1);
						gamemod_gui_searchupdate (getalias gamemod_gui_searchfield);
					] 0;
					guibutton " [?*RED()-?*CLEAR()]" [
						if (> (getalias gamemod_cfg_maxsearchelements) 0) [
							alias gamemod_cfg_maxsearchelements (- (getalias gamemod_cfg_maxsearchelements) 1);
							gamemod_gui_searchupdate (getalias gamemod_gui_searchfield);
						];
					] 0;
				];
			];
			guicheckbox "" gamemod_gui_servers_filter_open_hide 1 0 [if (getalias gamemod_gui_servers_filter_open_hide) [gamemod_filter_open_switch 0]];
			guicheckbox "" gamemod_cfg_gui_servers_show_clientcounts;
			guicheckbox "" gamemod_cfg_gui_servers_show_servercounts;
		];
	];
] "gamemod settings";

gamemod_showgui_settings = [
	gamemod_gui_quickconnecteditupdate;
	showgui gamemod_gui_settings;
];

?*IFGUIDEBUG()

newgui gamemod_gui_quickconnect_config [
	guinoautotab [
		guistayopen [
			if (getalias gamemod_gui_quickconnect_config_show_help) [
				guibutton "hide help?*PRIMCOLOR()<?*CLEAR()" [alias gamemod_gui_quickconnect_config_show_help 0] 0;
				guibar;
				guitext "?*WHITE()this may look a bit confusing, but it isn't actually:?*CLEAR()" 0;
				guitext "?*WHITE()quickconnect is an area where you can place your favorite servers.?*CLEAR()" 0;
				guitext "?*WHITE()quickconnect is built as follows:?*CLEAR()" 0;
				guitext "?*WHITE()?*PRIMCOLOR()-?*CLEAR() the quickconnect area contains rows ?*GREY()(arranged one below the other, separated by a horizontal bar)?*CLEAR()?*CLEAR()" 0;
				guitext "?*WHITE()?*PRIMCOLOR()-?*CLEAR() rows contain blocks ?*GREY()(arranged next to each other, separated by a vertical bar)?*CLEAR()?*CLEAR()" 0;
				guitext "?*WHITE()?*PRIMCOLOR()-?*CLEAR() blocks then contain servers ?*GREY()(arranged one below the other, not separated)?*CLEAR()?*CLEAR()" 0;
				guibar;
				guitext "?*WHITE()below is your quickconnect configuration:?*CLEAR()" 0;
				guitext "?*WHITE()the [?*GREEN()+?*CLEAR() row] button adds a row to the bottom of your quickconnect area, the [?*RED()-?*CLEAR() row] button removes the corresponding row.?*CLEAR()" 0;
				guitext "?*WHITE()the [?*GREEN()+?*CLEAR() block] button adds a block to the right of the row, the [?*RED()-?*CLEAR() block] button removes the corresponding block.?*CLEAR()" 0;
				guitext "?*WHITE()the [?*GREEN()+?*CLEAR()] button adds an unset server entry below the existing servers to the block, the [?*RED()-?*CLEAR()] button removes the corresponding server.?*CLEAR()" 0;
			] [
				guibutton "?*PRIMCOLOR()>?*CLEAR()show help" [alias gamemod_gui_quickconnect_config_show_help 1] 0;
			];
		];
		guibar;
		gamemod_guicode_quickconnectedit;
	];
] "quickconnect configuration";

?*IFGUIDEBUG()

gamemod_set_gui_servers = [
	alias gamemod_autorefreshing "";
	newgui servers [
		alias gamemod_c_last_refresh_before (- (getmillis) (getalias gamemod_lastupdatemillis));
		if (getalias gamemod_cfg_gui_servers_autorefresh) [
			if (&& (! (getalias gamemod_autorefreshing)) (> $gamemod_c_last_refresh_before (getalias gamemod_cfg_gui_servers_autorefresh_after))) [
				alias gamemod_autorefreshing 1;
				sleep 0 [updatefrommaster];
			];
		];
		guialign 0 [
			guistayopen [
				if (getalias gamemod_cfg_gui_servers_refresh_button_blink) [
					if (&& (> $gamemod_c_last_refresh_before (getalias gamemod_cfg_gui_servers_refresh_button_blink_after)) (< (mod (- $gamemod_c_last_refresh_before (getalias gamemod_cfg_gui_servers_refresh_button_blink_after)) (getalias gamemod_cfg_gui_servers_refresh_button_blink_interval)) (div (getalias gamemod_cfg_gui_servers_refresh_button_blink_interval) 2))) [
						guibutton "?*PRIMCOLOR()>refresh?*CLEAR()" [ updatefrommaster ] 0;
					] [
						guibutton "?*PRIMCOLOR()>?*CLEAR()refresh" [ updatefrommaster ] 0;
					];
				] [
					guibutton "?*PRIMCOLOR()>?*CLEAR()refresh" [ updatefrommaster ] 0;
				];
			];
			guibar;
			guibutton "?*PRIMCOLOR()>?*CLEAR()settings" [ gamemod_showgui_settings; ] 0;
			guibar;
			guibutton "?*PRIMCOLOR()>?*CLEAR()about" [ showgui gamemod_gui_about; ] 0;
			guibar;
			guibutton "?*PRIMCOLOR()>?*CLEAR()install" [ gamemod_showgui_install; ] 0;
			guibar;
			if (getalias gamemod_gui_searchfield_enable) [
				guistayopen [
				guibutton "[?*PRIMCOLOR()x?*CLEAR()] " [gamemod_gui_searchfield_enable = 0;] 0;
				];
				guifield gamemod_gui_searchfield 15 [gamemod_gui_searchupdate $gamemod_gui_searchfield];
			] [
				guistayopen [
					guibutton "?*PRIMCOLOR()>?*CLEAR()search" [gamemod_gui_searchfield_enable = 1;] 0;
				];
			];
			if (! (|| (getalias gamemod_gui_servers_filter_open_hide) (getalias gamemod_gui_servers_filter_open))) [
				guibar;
				guistayopen [
					guibutton "?*PRIMCOLOR()>?*CLEAR()open only" [gamemod_filter_open_switch 1] 0;
				];
			];
			if (getalias gamemod_cfg_gui_servers_show_clientcounts) [
				guibar;
				guitext (concatword "?*PRIMCOLOR()" (getalias gamemod_sumnumclients) "?*WHITE()/?*CLEAR()" (getalias gamemod_summaxclients) "?*CLEAR() ?*WHITE()clients?*CLEAR()") 0;
			];
			if (getalias gamemod_cfg_gui_servers_show_servercounts) [
				guibar;
				guitext (concatword "?*PRIMCOLOR()" (getalias gamemod_activeservers) "?*WHITE()/?*CLEAR()?*SERVERCOUNT()?*CLEAR() ?*WHITE()servers?*CLEAR()") 0;
			];
		];
		if (&& (getalias gamemod_gui_searchfield_enable) (> (strlen (getalias gamemod_gui_searchfield)) 0)) [
			guibar;
			gamemod_guicode_search;
		] [
			if (!=s (getalias gamemod_guicode_quickconnect) "") [
				guibar;
				gamemod_guicode_quickconnect;
			];
		];
		if (getalias gamemod_gui_servers_filter_open) [
			guibar;
			guialign 0 [
				guitext "?*WHITE()showing open servers only?*CLEAR()" 0;
				guibar;
				guistayopen [
					guibutton "?*PRIMCOLOR()>?*CLEAR()show all" [gamemod_filter_open_switch 0] 0;
				];
			];
		];
		guibar;
		gamemod_guipart_wrong_domain_warning;
		guistayopen [
			guiservers;
		];
		alias gamemod_c_last_refresh_before "";
	] "?*PRIMCOLOR()crapmod masterserver?*CLEAR() ?*GREY()?*VERSION()?*CLEAR()";
];

sleep 0 [gamemod_set_gui_servers];

?*IFGUIDEBUG()

gamemod_defaultcfg;
gamemod_onrequest;

?*IFGUIDEBUG()

?*SUMNUMCLIENTS(reset)
?*IFGUIDEBUG()
?*SUMMAXCLIENTS(reset)
?*IFGUIDEBUG()
?*SUMACTIVESERVERS(reset)
?*IFGUIDEBUG()
?*FORSERVERS(TILEOL WHOLE gamemod_as ?*IP() ?*PORT() [?*CLIENTS() ?*MAXCLIENTS() ?*MASTERMODE() ?*GAMEMODE() ?*REMAINING()] "?*MAP()" "?*ESCAPEDESC()" [?*ESCAPEDETAILPLAYERLIST()];?*SUMNUMCLIENTS()?*SUMMAXCLIENTS()?*SUMACTIVESERVERS())

?*IFGUIDEBUG(TILEOL gamemod_as 127.0.0.1 28785 [16 32 -1 0 300] "reissen" "trollserv" [];)

?*IFGUIDEBUG()
alias gamemod_sumnumclients ?*SUMNUMCLIENTS(out);
?*IFGUIDEBUG()
alias gamemod_summaxclients ?*SUMMAXCLIENTS(out);
?*IFGUIDEBUG()
alias gamemod_activeservers ?*SUMACTIVESERVERS(out);
?*IFGUIDEBUG()

gamemod_onfinishrequest;

?*IFGUIDEBUG()
?*GUISIZECALLF(alias gamemod_guisize %d;)
?*IFGUIDEBUG()
